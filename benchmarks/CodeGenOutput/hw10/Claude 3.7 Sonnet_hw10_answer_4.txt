(* Implementing eval function *)
let rec eval exp =
  match exp with
  (* Values evaluate to themselves *)
  | I _ -> exp
  | B _ -> exp
  | Fn _ -> exp

  (* This evaluator is _not_ environment-based. Variables should never
     appear during evaluation since they should be substituted away when
     eliminating binding constructs, e.g. function applications and lets.
     Therefore, if we encounter a variable, we raise an error.
  *)
  | Var x -> raise (Stuck (Free_variable x))

  (* Primitive operations: +, -, *, <, = *)
  | Primop (po, args) ->
      let args = List.map eval args in
      begin
        match eval_op po args with
        | None -> raise (Stuck Bad_primop_args)
        | Some v -> v
      end

  | If (e, e1, e2) ->
      begin
        match eval e with
        | B true -> eval e1
        | B false -> eval e2
        | _ -> raise (Stuck If_non_true_false)
      end

  | Let (x, e1, e2) ->
      let e1 = eval e1 in
      eval (subst (e1, x) e2)

  | Rec (f, _, e) -> 
      let rec_exp = eval (subst (exp, f) e) in
      rec_exp

  | Apply (e, es) -> 
      match eval e with
      | Fn (xs, body) -> 
          if List.length xs <> List.length es then
            raise (Stuck Arity_mismatch)
          else
            let evaled_args = List.map eval es in
            let subst_pairs = List.combine evaled_args (List.map fst xs) in
            eval (subst_list subst_pairs body)
      | _ -> raise (Stuck Apply_non_fn)

(* Implementing infer function *)
let rec infer ctx e =
  match e with
  | Var x ->
      begin
        try lookup x ctx
        with Not_found -> raise (TypeError (Free_variable x))
      end
  | I _ -> Int
  | B _ -> Bool

  | Primop (po, exps) ->
      let (domain, range) = primopType po in
      check ctx exps domain range

  | If (e, e1, e2) ->
      begin
        match infer ctx e with
        | Bool ->
            let t1 = infer ctx e1 in
            let t2 = infer ctx e2 in
            if t1 = t2 then t1
            else type_mismatch t1 t2
        | t -> type_mismatch Bool t
      end

  | Let (x, e1, e2) ->
      let t1 = infer ctx e1 in
      infer (extend ctx (x, t1)) e2

  | Rec (f, t, e) -> 
      let ctx' = extend ctx (f, t) in
      let t' = infer ctx' e in
      if t = t' then t
      else type_mismatch t t'

  | Fn (xs, e) -> 
      let param_types = List.map snd xs in
      let ctx' = extend_list ctx xs in
      let return_type = infer ctx' e in
      Arrow (param_types, return_type)

  | Apply (e, es) -> 
      match infer ctx e with
      | Arrow (param_types, return_type) ->
          if List.length param_types <> List.length es then
            raise (TypeError Arity_mismatch)
          else
            let arg_types = List.map (infer ctx) es in
            List.iter2 (fun expected actual -> 
              if expected <> actual then
                type_mismatch expected actual
            ) param_types arg_types;
            return_type
      | t -> raise (TypeError (Apply_non_arrow t))

and check ctx exps tps result =
  match exps, tps with
  | [], [] -> result
  | e :: es, t :: ts ->
      let t' = infer ctx e in
      if t = t' then check ctx es ts result
      else type_mismatch t t'
  | _ -> raise (TypeError Arity_mismatch)

(* Implementing unify function *)
let unify : utp -> utp -> utp UTVarMap.t =
  let rec unify (type_substitution : utp UTVarMap.t) (t1 : utp) (t2 : utp) : utp UTVarMap.t =
    match t1, t2 with
    (* Unifying identical concrete types does nothing *)
    | UInt, UInt
    | UBool, UBool -> type_substitution
    | UTVar a, UTVar a' when a = a' -> type_substitution

    (* For type constructors, recursively unify the parts *)
    | UArrow (t1, t1'), UArrow (t2, t2') ->
        let subst' = unify type_substitution t1 t2 in
        unify subst' t1' t2'

    | UCross ts1, UCross ts2 ->
        if List.length ts1 <> List.length ts2 then
          unif_error @@ UnifMismatch (t1, t2)
        else
          List.fold_left2 
            (fun subst t1 t2 -> unify subst t1 t2) 
            type_substitution ts1 ts2

    | UTVar a, _ -> unifyVar type_substitution a t2
    | _, UTVar b -> unifyVar type_substitution b t1
    (* All other cases are mismatched types. *)
    | _, _ -> unif_error @@ UnifMismatch (t1, t2)

  (* Unify a variable with a type *)
  and unifyVar (type_substitution : utp UTVarMap.t) (a : string) (t : utp) : utp UTVarMap.t =
    let rec occurs : utp -> bool = function
      | UInt | UBool -> false
      | UArrow (t1, t2) -> occurs t1 || occurs t2
      | UCross ts -> List.exists occurs ts
      | UTVar b ->
          if a = b
          then true
          else
            match UTVarMap.find_opt b type_substitution with
            | None -> false
            | Some t' -> occurs t'
    in
    if occurs t then
      unif_error @@ UnifOccurs (a, t)
    else
      match UTVarMap.find_opt a type_substitution with
      | Some t' -> unify type_substitution t' t
      | None -> UTVarMap.add a t type_substitution

  in
  fun t1 t2 -> unify UTVarMap.empty t1 t2