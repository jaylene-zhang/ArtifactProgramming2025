type nat = Z | S of nat

(* Q1a: Convert int to nat using tail recursion *)
let q1a_nat_of_int (n : int) : nat =
  (* TODO: Implement the function. *)
  let rec aux i acc =
    if i <= 0 then acc else aux (i - 1) (S acc)
  in
  aux n Z

(* Q1b: Convert nat to int using tail recursion *)
let q1b_int_of_nat (n : nat) : int =
  (* TODO: Implement the function. *)
  let rec aux nat acc =
    match nat with
    | Z -> acc
    | S t -> aux t (acc + 1)
  in
  aux n 0

(* Q1c: Add two nat values recursively *)
let rec q1c_add (n : nat) (m : nat) : nat =
  (* TODO: Implement the function. *)
  match n with
  | Z -> m
  | S n' -> S (q1c_add n' m)

type exp =
  | Const of float
  | Var
  | Plus of exp * exp
  | Times of exp * exp
  | Pow of exp * int

(* Q2a: Negate an expression using multiplication by -1.0 *)
let q2a_neg (e : exp) : exp =
  (* TODO: Implement the function. *)
  Times (Const (-1.0), e)

(* Q2b: Subtract one expression from another *)
let q2b_minus (e1 : exp) (e2 : exp) : exp =
  (* TODO: Implement the function. *)
  Plus (e1, q2a_neg e2)

(* Q2c: Divide one expression by another *)
let q2c_quot (e1 : exp) (e2 : exp) : exp =
  (* TODO: Implement the function. *)
  Times (e1, Pow (e2, -1))

(* Q3: Evaluate an expression given a value for Var *)
let rec eval (a : float) (e : exp) : float =
  (* TODO: Implement the function. *)
  match e with
  | Const x -> x
  | Var -> a
  | Plus (e1, e2) -> (eval a e1) +. (eval a e2)
  | Times (e1, e2) -> (eval a e1) *. (eval a e2)
  | Pow (e, n) -> (eval a e) ** (float_of_int n)

(* Q4: Symbolically differentiate an expression *)
let rec diff (e : exp) : exp =
  (* TODO: Implement the function. *)
  match e with
  | Const _ -> Const 0.0
  | Var -> Const 1.0
  | Plus (e1, e2) -> Plus (diff e1, diff e2)
  | Times (e1, e2) -> Plus (Times (diff e1, e2), Times (e1, diff e2))
  | Pow (e, n) ->
      if n = 0 then Const 0.0
      else Times (Times (Const (float_of_int n), Pow (e, n - 1)), diff e)