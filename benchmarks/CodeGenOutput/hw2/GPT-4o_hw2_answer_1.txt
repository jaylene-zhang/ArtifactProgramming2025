```ocaml
(* Q1 *)

(* TODO:  Implement {!q1a_nat_of_int} using a tail-recursive helper. *)
let q1a_nat_of_int (n : int) : nat =
  let rec helper (n : int) (acc : nat) : nat =
    if n <= 0 then acc else helper (n - 1) (S acc)
  in
  helper n Z

(* TODO:  Implement {!q1b_int_of_nat} using a tail-recursive helper. *)
let q1b_int_of_nat (n : nat) : int =
  let rec helper (n : nat) (acc : int) : int =
    match n with
    | Z -> acc
    | S rest -> helper rest (acc + 1)
  in
  helper n 0

(* TODO: Implement {!q1c_add}. *)
let rec q1c_add (n : nat) (m : nat) : nat =
  match n with
  | Z -> m
  | S rest -> S (q1c_add rest m)

(* Q2 *)

type exp =
  | Const of float
  | Var
  | Plus of exp * exp
  | Times of exp * exp
  | Pow of exp * int

(* TODO: Implement {!q2a_neg}. *)
let q2a_neg (e : exp) : exp =
  Times (Const (-1.0), e)

(* TODO: Implement {!q2b_minus}. *)
let q2b_minus (e1 : exp) (e2 : exp) : exp =
  Plus (e1, q2a_neg e2)

(* TODO: Implement {!q2c_quot}. *)
let q2c_quot (e1 : exp) (e2 : exp) : exp =
  Times (e1, Pow (e2, -1))

(* Q3 *)

(* TODO: Implement {!eval}. *)
let rec eval (a : float) (e : exp) : float =
  match e with
  | Const c -> c
  | Var -> a
  | Plus (e1, e2) -> eval a e1 +. eval a e2
  | Times (e1, e2) -> eval a e1 *. eval a e2
  | Pow (e, n) -> (eval a e) ** float_of_int n

(* Q4 *)

(* TODO: Implement {!diff}. *)
let rec diff (e : exp) : exp =
  match e with
  | Const _ -> Const 0.0
  | Var -> Const 1.0
  | Plus (e1, e2) -> Plus (diff e1, diff e2)
  | Times (e1, e2) -> Plus (Times (diff e1, e2), Times (e1, diff e2))
  | Pow (e, n) ->
    Times (Times (Const (float_of_int n), Pow (e, n - 1)), diff e)
```