```ocaml
(* TODO: Implement the missing cases of free_variables. *)
let rec free_variables : exp -> name list =
  let union l1 l2 = delete l2 l1 @ l2 in
  let union_fvs es =
    List.fold_left (fun acc exp -> union acc (free_variables exp)) [] es
  in
  function
  | Var y -> [y]
  | I _ | B _ -> []
  | If(e, e1, e2) -> union_fvs [e; e1; e2]
  | Primop (_, args) -> union_fvs args
  | Fn (xs, e) ->
      let bound_vars = List.map fst xs in
      let free_in_body = free_variables e in
      delete bound_vars free_in_body
  | Rec (x, _, e) ->
      delete [x] (free_variables e)
  | Let (x, e1, e2) ->
      let free_in_e1 = free_variables e1 in
      let free_in_e2 = delete [x] (free_variables e2) in
      union free_in_e1 free_in_e2
  | Apply (e, es) -> union_fvs (e :: es)

(* TODO: Implement the missing cases of unused_vars. *)
let rec unused_vars =
  function
  | Var _ | I _ | B _ -> []
  | If (e, e1, e2) -> unused_vars e @ unused_vars e1 @ unused_vars e2
  | Primop (_, args) ->
      List.fold_left (fun acc exp -> acc @ unused_vars exp) [] args
  | Let (x, e1, e2) ->
      let unused = unused_vars e1 @ unused_vars e2 in
      if List.mem x (free_variables e2) then
        unused
      else
        x :: unused
  | Rec (x, _, e) ->
      if List.mem x (free_variables e) then
        unused_vars e
      else
        x :: unused_vars e
  | Fn (xs, e) ->
      let bound_vars = List.map fst xs in
      let unused_in_body = unused_vars e in
      let unused_in_bound = List.filter (fun x -> not (List.mem x (free_variables e))) bound_vars in
      unused_in_bound @ unused_in_body
  | Apply (e, es) ->
      unused_vars e @ List.fold_left (fun acc arg -> acc @ unused_vars arg) [] es

(* TODO: Implement the missing cases of subst. *)
let rec subst ((e', x) as s) exp =
  match exp with
  | Var y ->
      if x = y then e'
      else Var y
  | I n -> I n
  | B b -> B b
  | Primop (po, args) -> Primop (po, List.map (subst s) args)
  | If (e, e1, e2) ->
      If (subst s e, subst s e1, subst s e2)
  | Let (y, e1, e2) ->
      let e1' = subst s e1 in
      if y = x then
        Let (y, e1', e2)
      else
        let (y, e2) =
          if List.mem y (free_variables e') then
            rename y e2
          else
            (y, e2)
        in
        Let (y, e1', subst s e2)
  | Rec (y, t, e) ->
      if y = x then
        Rec (y, t, e)
      else
        let (y, e) =
          if List.mem y (free_variables e') then
            rename y e
          else
            (y, e)
        in
        Rec (y, t, subst s e)
  | Fn (xs, e) ->
      let bound_vars = List.map fst xs in
      if List.mem x bound_vars then
        Fn (xs, e)
      else
        let (new_bound_vars, e) =
          if List.exists (fun y -> List.mem y (free_variables e')) bound_vars then
            rename_all bound_vars e
          else
            (bound_vars, e)
        in
        let new_params = List.combine new_bound_vars (List.map snd xs) in
        Fn (new_params, subst s e)
  | Apply (e, es) ->
      Apply (subst s e, List.map (subst s) es)
```