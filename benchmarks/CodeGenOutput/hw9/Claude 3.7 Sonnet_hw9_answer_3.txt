let rec free_variables : exp -> name list =
  let union l1 l2 = delete l2 l1 @ l2 in
  let union_fvs es =
    List.fold_left (fun acc exp -> union acc (free_variables exp)) [] es
  in
  function
  | Var y -> [y]
  | I _ | B _ -> []
  | If(e, e1, e2) -> union_fvs [e; e1; e2]
  | Primop (_, args) -> union_fvs args
  | Fn (xs, e) ->
      let vars = List.map fst xs in
      delete vars (free_variables e)
  | Rec (x, _, e) ->
      delete [x] (free_variables e)
  | Let (x, e1, e2) ->
      union (free_variables e1) (delete [x] (free_variables e2))
  | Apply (e, es) -> union (free_variables e) (union_fvs es)

let rec unused_vars =
  function
  | Var _ | I _ | B _ -> []
  | If (e, e1, e2) -> unused_vars e @ unused_vars e1 @ unused_vars e2
  | Primop (_, args) ->
      List.fold_left (fun acc exp -> acc @ unused_vars exp) [] args
  | Let (x, e1, e2) ->
      let unused = unused_vars e1 @ unused_vars e2 in
      if List.mem x (free_variables e2) then
        unused
      else
        x :: unused
  | Rec (x, _, e) ->
      let unused = unused_vars e in
      if List.mem x (free_variables e) then
        unused
      else
        x :: unused
  | Fn (xs, e) ->
      let unused = unused_vars e in
      List.fold_left 
        (fun acc (name, _) -> 
          if List.mem name (free_variables e) then acc
          else name :: acc)
        unused xs
  | Apply (e, es) ->
      unused_vars e @ List.fold_left (fun acc exp -> acc @ unused_vars exp) [] es

let rec subst ((e', x) as s) exp =
  match exp with
  | Var y ->
      if x = y then e'
      else Var y
  | I n -> I n
  | B b -> B b
  | Primop (po, args) -> Primop (po, List.map (subst s) args)
  | If (e, e1, e2) ->
      If (subst s e, subst s e1, subst s e2)
  | Let (y, e1, e2) ->
      let e1' = subst s e1 in
      if y = x then
        Let (y, e1', e2)
      else
        let (y, e2) =
          if List.mem y (free_variables e') then
            rename y e2
          else
            (y, e2)
        in
        Let (y, e1', subst s e2)
  | Rec (y, t, e) ->
      if y = x then
        Rec (y, t, e)
      else
        let (y, e) =
          if List.mem y (free_variables e') then
            rename y e
          else
            (y, e)
        in
        Rec (y, t, subst s e)
  | Fn (xs, e) ->
      let names = List.map fst xs in
      if List.mem x names then
        Fn (xs, e)
      else
        let fvs = free_variables e' in
        let shadowing_names = List.filter (fun name -> List.mem name fvs) names in
        if shadowing_names = [] then
          Fn (xs, subst s e)
        else
          let (names', e') = rename_all shadowing_names e in
          let xs' = List.map2 (fun (_, t) name -> (name, t))
                     (List.filter (fun (name, _) -> List.mem name shadowing_names) xs)
                     names' in
          let xs'' = List.map (fun (name, t) -> 
                      if List.mem name shadowing_names then 
                        List.find (fun (n, _) -> n = name) xs'
                      else
                        (name, t)) xs in
          Fn (xs'', subst s e')
  | Apply (e, es) ->
      Apply (subst s e, List.map (subst s) es)