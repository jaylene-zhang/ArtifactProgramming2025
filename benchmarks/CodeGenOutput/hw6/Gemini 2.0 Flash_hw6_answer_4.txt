```ocaml
(* TODO: Implement the function. *)

(* Q1: 5 points *)
let rec traverse (f : 'a -> 'b option) (l : 'a list) : 'b list option =
  match l with
  | [] -> Some []
  | x :: xs ->
      match f x with
      | None -> None
      | Some y -> (
          match traverse f xs with
          | None -> None
          | Some ys -> Some (y :: ys))

(* TODO: Implement the functions Q2.1 *)
(* Q2.1: 2 points *)
let ap (fm : ('a -> 'b) Maybe.t) (mx : 'a Maybe.t) : 'b Maybe.t =
  let open Maybe in
  match (fm, mx) with
  | Just f, Just x -> Just (f x)
  | _, _ -> Nothing

(* TODO: Implement the functions Q2.2 *)
(* Q2.2: 2 points *)
let seq2 (m1 : 'a1 Maybe.t) (m2 : 'a2 Maybe.t) : ('a1 * 'a2) Maybe.t =
  let open Maybe in
  match (m1, m2) with
  | Just x, Just y -> Just (x, y)
  | _, _ -> Nothing

(* TODO: Implement the functions Q2.3 *)
(* Q2.3: 3 points *)
let seq_predicate2
    (p1 : 'a1 -> 'b1 Maybe.t)
    (p2 : 'a2 -> 'b2 Maybe.t) : ('a1 * 'a2) -> ('b1 * 'b2) Maybe.t =
 fun (a1, a2) ->
  let open Maybe in
  match (p1 a1, p2 a2) with
  | Just b1, Just b2 -> Just (b1, b2)
  | _, _ -> Nothing

(* TODO: Implement the functions Q2.4 *)
(* Q2.4: 3 points *)
let map (f : 'a -> 'b) (a : 'a Maybe.t) : 'b Maybe.t =
  let open Maybe in
  match a with Just x -> Just (f x) | Nothing -> Nothing

(* TODO: Implement the functions Q2.5 *)
(* Q2.5: 10 points *)
let rec traverse' (f : 'a -> 'b Maybe.t) (l : 'a list) : 'b list Maybe.t =
  let open Maybe in
  match l with
  | [] -> return []
  | x :: xs ->
      f x
      >>= fun y ->
      traverse' f xs
      >>= fun ys -> return (y :: ys)

(* Given *)

(* Q3 *)

type nat = Z | S of nat
type 'a church = 'a -> ('a -> 'a) -> 'a

(* type 'a option = Some of 'a | None *)
type ('a, 'r) coption = ('a -> 'r) -> (unit -> 'r) -> 'r

(* type 'a list = (::) of 'a * 'a list | [] *)
type ('a, 'r) clist =
  | CList of (('a -> ('a, 'r) clist -> 'r) -> (unit -> 'r) -> 'r)
  (* OCaml does not support circular types. The circularity is guarded by
     the constructor [CList]. *)

(* TODO: Implement the function Q3.1a *)

(* Q3.1a: 2 points *)
let church_to_nat (c : 'a church) : nat =
  c Z (fun x -> S x)

(* TODO: Implement the function Q3.1b *)

(* Q3.1b: 3 points *)
let rec nat_to_church (n : nat) : 'a church =
  match n with Z -> fun x _ -> x | S n' -> fun x f -> f (nat_to_church n' x f)

(* TODO: Implement the function Q3.2a *)

(* Q3.2a: 2 points *)
let coption_to_option (copt : ('a, 'r) coption) : 'a option =
  copt (fun x -> Some x) (fun () -> None)

(* TODO: Implement the function Q3.2b *)

(* Q3.2b: 3 points *)
let option_to_coption (m : 'a option) : ('a, 'r) coption =
 fun success failure ->
  match m with Some x -> success x | None -> failure ()

(* TODO: Implement the function Q3.3a *)

(* Q3.3a: 2 points*)
let rec clist_to_list ((CList clist) : ('a, 'r) clist) : 'a list =
  clist
    (fun x xs -> x :: clist_to_list xs)
    (fun () -> [])

(* TODO: Implement the function Q3.3b *)

(* Q3.3b: 3 points *)
let rec list_to_clist (l : 'a list) : ('a, 'r) clist =
  CList
    (fun cons nil ->
      match l with
      | [] -> nil ()
      | x :: xs -> cons x (list_to_clist xs))

(* Q4: 5 points *)
let rec map_clist (f : 'a -> 'b) (clist : ('a, 'r) clist) : ('b, 'r) clist =
  match clist with
  | CList clist_impl ->
      CList
        (fun cons nil ->
          clist_impl
            (fun a clist_rest -> cons (f a) (map_clist f clist_rest))
            (fun () -> nil ()))
```