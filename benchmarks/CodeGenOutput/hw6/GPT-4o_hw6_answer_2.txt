```ocaml
(* Q1: 5 points *)
let rec traverse (f : 'a -> 'b option) (l : 'a list) : 'b list option =
  match l with
  | [] -> Some []
  | x :: xs -> (
      match f x with
      | None -> None
      | Some y -> (
          match traverse f xs with
          | None -> None
          | Some ys -> Some (y :: ys)))

(* Q2.1: 2 points *)
let ap (fm : ('a -> 'b) Maybe.t) (mx : 'a Maybe.t) : 'b Maybe.t =
  let open Maybe in
  bind fm (fun f -> bind mx (fun x -> return (f x)))

(* Q2.2: 2 points *)
let seq2 (m1 : 'a1 Maybe.t) (m2 : 'a2 Maybe.t) : ('a1 * 'a2) Maybe.t =
  let open Maybe in
  bind m1 (fun x1 -> bind m2 (fun x2 -> return (x1, x2)))

(* Q2.3: 3 points *)
let seq_predicate2
    (p1 : 'a1 -> 'b1 Maybe.t)
    (p2 : 'a2 -> 'b2 Maybe.t) :
    ('a1 * 'a2) -> ('b1 * 'b2) Maybe.t =
  fun (x1, x2) ->
    let open Maybe in
    bind (p1 x1) (fun y1 -> bind (p2 x2) (fun y2 -> return (y1, y2)))

(* Q2.4: 3 points *)
let map (f : 'a -> 'b) (a : 'a Maybe.t) : 'b Maybe.t =
  let open Maybe in
  bind a (fun x -> return (f x))

(* Q2.5: 10 points *)
let rec traverse' (f : 'a -> 'b Maybe.t) (l : 'a list) : 'b list Maybe.t =
  let open Maybe in
  match l with
  | [] -> return []
  | x :: xs ->
      bind (f x) (fun y -> bind (traverse' f xs) (fun ys -> return (y :: ys)))

(* Q3.1a: 2 points *)
let church_to_nat (c : 'a church) : nat =
  c Z (fun acc -> S acc)

(* Q3.1b: 3 points *)
let rec nat_to_church (n : nat) : 'a church =
  match n with
  | Z -> (fun z _ -> z)
  | S n' -> (fun z s -> s (nat_to_church n' z s))

(* Q3.2a: 2 points *)
let coption_to_option (copt : ('a, 'r) coption) : 'a option =
  copt (fun x -> Some x) (fun () -> None)

(* Q3.2b: 3 points *)
let option_to_coption (m : 'a option) : ('a, 'r) coption =
  match m with
  | None -> fun _ none -> none ()
  | Some x -> fun some _ -> some x

(* Q3.3a: 2 points *)
let rec clist_to_list ((CList clist) : ('a, 'r) clist) : 'a list =
  clist (fun hd tl -> hd :: clist_to_list tl) (fun () -> [])

(* Q3.3b: 3 points *)
let rec list_to_clist (l : 'a list) : ('a, 'r) clist =
  match l with
  | [] -> CList (fun _ none -> none ())
  | x :: xs ->
      CList (fun cons none -> cons x (list_to_clist xs))

(* Q4: 5 points *)
let rec map_clist (f : 'a -> 'b) (clist : ('a, 'r) clist) : ('b, 'r) clist =
  match clist with
  | CList c ->
      CList (fun cons none ->
        c
          (fun x xs -> cons (f x) (map_clist f xs))
          (fun () -> none ()))
```