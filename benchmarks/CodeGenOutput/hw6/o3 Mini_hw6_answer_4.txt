(* Q1: Traverse (5 points) *)
let rec traverse (f : 'a -> 'b option) (l : 'a list) : 'b list option =
  (* TODO: Implement the function. *)
  match l with
  | [] -> Some []
  | x :: xs ->
      (match f x with
       | None -> None
       | Some y ->
           (match traverse f xs with
            | None -> None
            | Some ys -> Some (y :: ys))))

(* Q2.1: Calling Functions under Maybe.t (2 points) *)
let ap (mx : 'a Maybe.t) (fm : ('a -> 'b) Maybe.t) : 'b Maybe.t =
  (* TODO: Implement the function. *)
  let open Maybe in
  fm >>= (fun f -> mx >>= (fun x -> return (f x)))

(* Q2.2: Tuple under Maybe.t (2 points) *)
let seq2 (m1 : 'a1 Maybe.t) (m2 : 'a2 Maybe.t) : ('a1 * 'a2) Maybe.t =
  (* TODO: Implement the function. *)
  let open Maybe in
  m1 >>= (fun a -> m2 >>= (fun b -> return (a, b)))

(* Q2.3: Generalize 2.2 (3 points) *)
let seq_predicate2
    (p1 : 'a1 -> 'b1 Maybe.t)
    (p2 : 'a2 -> 'b2 Maybe.t) : ('a1 * 'a2) -> ('b1 * 'b2) Maybe.t =
  (* TODO: Implement the function. *)
  fun (x, y) ->
    let open Maybe in
    p1 x >>= (fun b1 ->
    p2 y >>= (fun b2 ->
    return (b1, b2)))

(* Q2.4: Map for Maybe.t (3 points) *)
let map (f : 'a -> 'b) (a : 'a Maybe.t) : 'b Maybe.t =
  (* TODO: Implement the function. *)
  let open Maybe in
  a >>= (fun x -> return (f x))

(* Q2.5: traverse' for Maybe.t (10 points) *)
let rec traverse' (f : 'a -> 'b Maybe.t) (l : 'a list) : 'b list Maybe.t =
  (* TODO: Implement the function. *)
  let open Maybe in
  match l with
  | [] -> return []
  | x :: xs ->
      f x >>= fun y ->
      traverse' f xs >>= fun ys ->
      return (y :: ys)

(* Q3: Conversions *)

(* Given types *)
type nat = Z | S of nat
type 'a church = 'a -> ('a -> 'a) -> 'a

type ('a, 'r) coption = ('a -> 'r) -> (unit -> 'r) -> 'r

type ('a, 'r) clist =
  | CList of (('a -> ('a, 'r) clist -> 'r) -> (unit -> 'r) -> 'r)

(* Q3.1a: church_to_nat (2 points) *)
let church_to_nat (c : 'a church) : nat =
  (* TODO: Implement the function. *)
  c Z (fun n -> S n)

(* Q3.1b: nat_to_church (3 points) *)
let rec nat_to_church (n : nat) : 'a church =
  (* TODO: Implement the function. *)
  fun z s ->
    match n with
    | Z -> z
    | S n' -> s ((nat_to_church n') z s)

(* Q3.2a: coption_to_option (2 points) *)
let coption_to_option (copt : ('a, 'r) coption) : 'a option =
  (* TODO: Implement the function. *)
  copt (fun x -> Some x) (fun () -> None)

(* Q3.2b: option_to_coption (3 points) *)
let option_to_coption (m : 'a option) : ('a, 'r) coption =
  (* TODO: Implement the function. *)
  match m with
  | Some x -> fun some nothing -> some x
  | None -> fun some nothing -> nothing ()

(* Q3.3a: clist_to_list (2 points) *)
let rec clist_to_list ((CList clist) : ('a, 'r) clist) : 'a list =
  (* TODO: Implement the function. *)
  clist (fun x xs -> x :: clist_to_list xs) (fun () -> [])

(* Q3.3b: list_to_clist (3 points) *)
let rec list_to_clist (l : 'a list) : ('a, 'r) clist =
  (* TODO: Implement the function. *)
  match l with
  | [] -> CList (fun _cons nil -> nil ())
  | x :: xs -> CList (fun cons nil -> cons x (list_to_clist xs))

(* Q4: Map for clist (5 points) *)
let rec map_clist (f : 'a -> 'b) (clist : ('a, 'r) clist) : ('b, 'r) clist =
  (* TODO: Implement the function. *)
  match clist with
  | CList cl ->
      CList (fun cons nil ->
        cl (fun x xs -> cons (f x) (map_clist f xs)) nil)