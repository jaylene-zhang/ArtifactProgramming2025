```ocaml
module Variable_set = Set.Make (String)
module Variable_map = Map.Make (String)

type formula =
  | Variable of string
  | Conjunction of formula * formula
  | Disjunction of formula * formula
  | Negation of formula

type truth_assignment = bool Variable_map.t

exception Unassigned_variable of string
exception Unsatisfiable_formula

(* TODO: Implement the function. *)
let collect_variables (formula : formula) : Variable_set.t =
  let rec aux acc formula =
    match formula with
    | Variable x -> Variable_set.add x acc
    | Conjunction (f1, f2) 
    | Disjunction (f1, f2) -> aux (aux acc f1) f2
    | Negation f -> aux acc f
  in
  aux Variable_set.empty formula

(* TODO: Implement the function. *)
let eval (state : truth_assignment) (formula : formula) : bool =
  let rec aux formula =
    match formula with
    | Variable x -> (
        match Variable_map.find_opt x state with
        | Some value -> value
        | None -> raise (Unassigned_variable x))
    | Conjunction (f1, f2) -> (aux f1) && (aux f2)
    | Disjunction (f1, f2) -> (aux f1) || (aux f2)
    | Negation f -> not (aux f)
  in
  aux formula

(* TODO: Implement the function. *)
let find_satisfying_assignment (formula : formula) : truth_assignment =
  let vars = Variable_set.elements (collect_variables formula) in
  let rec backtrack assignment remaining_vars =
    match remaining_vars with
    | [] -> if eval assignment formula then assignment else raise Unsatisfiable_formula
    | var :: rest_vars ->
        try
          backtrack (Variable_map.add var true assignment) rest_vars
        with Unsatisfiable_formula ->
          backtrack (Variable_map.add var false assignment) rest_vars
  in
  backtrack Variable_map.empty vars
```