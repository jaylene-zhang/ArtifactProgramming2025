let rec collect_variables (formula : formula) : Variable_set.t =
  match formula with
  | Variable x -> Variable_set.singleton x
  | Conjunction (f1, f2)
  | Disjunction (f1, f2) ->
      Variable_set.union (collect_variables f1) (collect_variables f2)
  | Negation f -> collect_variables f

let rec eval (state : truth_assignment) (formula : formula) : bool =
  match formula with
  | Variable x ->
      (match Variable_map.find_opt x state with
       | Some b -> b
       | None -> raise (Unassigned_variable x))
  | Conjunction (f1, f2) -> eval state f1 && eval state f2
  | Disjunction (f1, f2) -> eval state f1 || eval state f2
  | Negation f -> not (eval state f)

let find_satisfying_assignment (formula : formula) : truth_assignment =
  let vars = Variable_set.elements (collect_variables formula) in
  let rec backtrack vs partial =
    match vs with
    | [] ->
        if eval partial formula then partial
        else raise Unsatisfiable_formula
    | v :: rest ->
        let partial_true = Variable_map.add v true partial in
        try backtrack rest partial_true with
        | Unsatisfiable_formula ->
            let partial_false = Variable_map.add v false partial in
            backtrack rest partial_false
  in
  backtrack vars Variable_map.empty