```ocaml
module Variable_set = Set.Make (String)
module Variable_map = Map.Make (String)

type formula =
  | Variable of string
  | Conjunction of formula * formula
  | Disjunction of formula * formula
  | Negation of formula

type truth_assignment = bool Variable_map.t

exception Unassigned_variable of string
exception Unsatisfiable_formula

(* TODO: Implement the function. *)
let collect_variables (formula : formula) : Variable_set.t =
  let rec aux formula =
    match formula with
    | Variable v -> Variable_set.singleton v
    | Conjunction (f1, f2) | Disjunction (f1, f2) ->
        Variable_set.union (aux f1) (aux f2)
    | Negation f -> aux f
  in
  aux formula

(* TODO: Implement the function. *)
let eval (state : truth_assignment) (formula : formula) : bool =
  let rec aux formula =
    match formula with
    | Variable v -> (
        match Variable_map.find_opt v state with
        | Some value -> value
        | None -> raise (Unassigned_variable v))
    | Conjunction (f1, f2) -> aux f1 && aux f2
    | Disjunction (f1, f2) -> aux f1 || aux f2
    | Negation f -> not (aux f)
  in
  aux formula

(* TODO: Implement the function. *)
let find_satisfying_assignment (formula : formula) : truth_assignment =
  let variables = Variable_set.elements (collect_variables formula) in
  let rec backtrack assignment unassigned =
    match unassigned with
    | [] -> if eval assignment formula then assignment else raise Unsatisfiable_formula
    | var :: rest ->
        try backtrack (Variable_map.add var true assignment) rest
        with Unsatisfiable_formula ->
          try backtrack (Variable_map.add var false assignment) rest
          with Unsatisfiable_formula -> raise Unsatisfiable_formula
  in
  backtrack Variable_map.empty variables
```