```ocaml
type 'a stream = 
  { head : 'a
  ; tail : unit -> 'a stream 
  }

let rec take (n : int) (s : 'a stream) : 'a list =
  if n <= 0 then []
  else s.head :: take (n - 1) (s.tail ())

let rec drop (n : int) (s : 'a stream) : 'a stream =
  if n <= 0 then s
  else drop (n - 1) (s.tail ())

let rec unfold (f : 'a -> 'b * 'a) (seed : 'a) : 'b stream =
  let (x, seed') = f seed in
  { head = x
  ; tail = fun () -> unfold f seed'
  }

let zeroes : int stream =
  unfold (fun x -> (0, x)) 0

let natural_numbers : int stream =
  unfold (fun x -> (x, x + 1)) 0

let even_numbers : int stream =
  unfold (fun x -> (x, x + 2)) 0

let map (f : 'a -> 'b) (s : 'a stream) : 'b stream =
  unfold (fun s -> (f s.head, (s.tail ()))) s

let rec find (p : 'a -> bool) (s : 'a stream) : ('a * 'a stream) =
  if p s.head then (s.head, s.tail ()) else find p (s.tail ())

let filter (p : 'a -> bool) (s : 'a stream) : 'a stream =
  unfold (find p) s

let zip (s1 : 'a stream) (s2 : 'b stream) : ('a * 'b) stream =
  unfold
    (fun (s1, s2) ->
       ((s1.head, s2.head), (s1.tail (), s2.tail ()))
    )
    (s1, s2)

let map2 (f : 'a -> 'b -> 'c) (s1 : 'a stream) (s2 : 'b stream) : 'c stream =
  unfold
    (fun (s1, s2) ->
       (f s1.head s2.head, (s1.tail (), s2.tail ())))
    (s1, s2)

let fibonacci : int stream =
  unfold (fun (a, b) -> (a, (b, a + b))) (0, 1)

let lucas : int stream =
  unfold (fun (a, b) -> (a, (b, a + b))) (2, 1)
```